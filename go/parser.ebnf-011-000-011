/*

ebnf2y -M -iy 1 -wr 1000 -o parser.y go1.1.1.ebnf:50+20=70

...............................................................................
named tokens, Binary_op, Unary_op

ebnf2y -M -iy 1 -wr 1000 -o parser.y parser.ebnf:80+22=102

...............................................................................
name categories, identifier_list, ...

ebnf2y -M -iy 1 -wr 1000 -o parser.y parser.ebnf:	 8+10	= 18
ebnf2y -M -wr 1000 -o parser.y parser.ebnf:		39+ 1	= 40
ebnf2y -M -wr 0 -o parser.y parser.ebnf:		21+10	= 31
ebnf2y -M -ws 0 -o parser.y parser.ebnf:		86+ 1	= 87
ebnf2y -M -o parser.y parser.ebnf:			21+10	= 31

...............................................................................
chancomm, commchan

ebnf2y -M -iy 1 -wr 1000 -o parser.y parser.ebnf:	 8+1	=  9
ebnf2y -M -iy 1 -o parser.y parser.ebnf:		 6+2	=  8
ebnf2y -M -wr 1000 -o parser.y parser.ebnf:		18+1	= 19
ebnf2y -M -wr 0 -o parser.y parser.ebnf:		18+1	= 19
ebnf2y -M -ws 0 -o parser.y parser.ebnf:		86+1	= 87
ebnf2y -M -o parser.y parser.ebnf:			18+1	= 19

...............................................................................
MethodDecl

ebnf2y -M -iy 1 -wr 1000 -o parser.y parser.ebnf:	8+1	= 9

...............................................................................
MethodSpec

ebnf2y -M -iy 1 -wr 1000 -o parser.y parser.ebnf:	 8+1	=  9
ebnf2y -M -wr 1 -o parser.y parser.ebnf:		18+1	= 19

...............................................................................
ReceiverType

ebnf2y -M -iy 1 -wr 1000 -o parser.y parser.ebnf:	15+0	= 15
ebnf2y -M -wr 1 -o parser.y parser.ebnf: 		19+0	= 19
ebnf2y -M -wr 1000 -o parser.y parser.ebnf:	 	19+0	= 19
ebnf2y -M -iy 1 -wr 0 -ws 1000 -o parser.y parser.ebnf	15+1	= 16
-M -wr 0 -ws 1000 -o parser.y parser.ebnf:		19+0	= 19

...............................................................................
ForClause

ebnf2y -M -iy 1 -wr 1000 -o parser.y parser.ebnf:	12+0	= 12
ebnf2y -M -wr 1000 -o parser.y parser.ebnf:		16+0	= 16

...............................................................................
Result, Signature

ebnf2y -M -iy 1 -o parser.y parser.ebnf:	11+0	= 11
ebnf2y -M -o parser.y parser.ebnf:		 6+0	=  6
*/

// ============================================================================

andand			= "&&" .
andnot			= "&^" .
chancomm		= "chan" "<-" .
colas			= ":=" .
comm			= "<-".
commchan		= "<-" "chan" .
ddd			= "..." .
dec			= "--" .
eq			= "==" .
ge			= ">=" .
inc			= "++" .
le			= "<=" .
lsh			= "<<" .
ne			= "!=" .
oror			= "||" .
rsh			= ">>" .

Binary_op = oror
	| andand
	| eq
	| ne
	| "<"
	| le
	| ">"
	| ge
	| "+"
	| "-"
	| "|"
	| "^"
	| "*"
	| "/"
	| "%"
	| lsh
	| rsh
	| "&"
	| andnot .

Unary_op = "+"
	| "-"
	| "!"
	| "^"
	| "*"
	| "&"
	| comm .

identifier_type 	= . // known to denote in current scope a local type, possibly qualified
identifier_built_in 	= . // known to denote in current scope a built-in (new, cap, len, ...)
identifier_field	= . // known to denote in current scope _and_ context (Key) a field name
identifier_list		= . // from left (decl) or right (colas) context
identifier_label	= . // ident ":" . while in StatementList
identifier_method	= . // known method name after Receiver "."
identifier_operand	= . // known var|const|fn name, possibly qualified
identifier_selector	= . // know ctx field|method name. Not after package_name "." .

name			= . // name being declared from context
	// FunctionDecl = "func" name - left ctx
	// ImportSpec - left ctx "import"
	// PackageClause - left ctx "package"
	// Receiver - left ctx "func" "("
	// TypeSpec - in TypeDecl
	// MethodDecl - left ctx "func" Receiver
	// MethodSpec - left ctx "interface"

// ----------------------------------------------------------------------------

add_op = "+"
	| "-"
	| "|"
	| "^" .
assign_op = [ add_op | mul_op ] "=" .
big_u_value = "\\" "U" hex_digit hex_digit hex_digit hex_digit hex_digit hex_digit hex_digit hex_digit .

//binary_op = "||"
//	| "&&"
//	| rel_op
//	| add_op
//	| mul_op .

byte_value = octal_byte_value | hex_byte_value .
decimal_digit = "0" … "9" .
decimal_lit = ( "1" … "9" ) { decimal_digit } .
decimals = decimal_digit { decimal_digit } .
escaped_char = "\\" (
		  "a"
		| "b"
		| "f"
		| "n"
		| "r"
		| "t"
		| "v"
		| "\\"
		| "'"
		| "\""
	  ) .
exponent = ( "e" | "E" ) [ "+" | "-" ] decimals .
float_lit = decimals "." [ decimals ] [ exponent ]
	| decimals exponent
	| "." decimals [ exponent ] .
hex_byte_value = "\\" "x" hex_digit hex_digit .
hex_digit = "0" … "9"
	| "A" … "F"
	| "a" … "f" .
hex_lit = "0" ( "x" | "X" ) hex_digit { hex_digit } .

//identifier = letter { letter | unicode_digit } .

imaginary_lit = ( decimals | float_lit ) "i" .
int_lit = decimal_lit
	| octal_lit
	| hex_lit .
interpreted_string_lit = "\"" { unicode_value | byte_value } "\"" .

//letter = unicode_letter | "_" .

little_u_value = "\\" "u" hex_digit hex_digit hex_digit hex_digit .

//mul_op = "*"
//	| "/"
//	| "%"
//	| "<<"
//	| ">>"
//	| "&"
//	| "&^" .
mul_op = "*"
	| "/"
	| "%"
	| lsh
	| rsh
	| "&"
	| andnot .

newline = .
octal_byte_value = "\\" octal_digit octal_digit octal_digit .
octal_digit = "0" … "7" .
octal_lit = "0" { octal_digit } .
raw_string_lit = "`" { unicode_char | newline } "`" .

//rel_op = "=="
//	| "!="
//	| "<"
//	| "<="
//	| ">"
//	| ">=" .

rune_lit = "'" ( unicode_value | byte_value ) "'" .
string_lit = raw_string_lit | interpreted_string_lit .

//unary_op = "+"
//	| "-"
//	| "!"
//	| "^"
//	| "*"
//	| "&"
//	| "<-" .

unicode_char = .

//unicode_digit = .
//unicode_letter = .

unicode_value = unicode_char
	| little_u_value
	| big_u_value
	| escaped_char .

AnonymousField = [ "*" ] TypeName .

//ArgumentList = ExpressionList [ "..." ] .
ArgumentList = ExpressionList [ ddd ] .

ArrayLength = Expression .
ArrayType = "[" ArrayLength "]" ElementType .
Assignment = ExpressionList assign_op ExpressionList .
BaseType = Type .
BaseTypeName = identifier_type .
BasicLit = int_lit
	| float_lit
	| imaginary_lit
	| rune_lit
	| string_lit .
Block = "{" StatementList "}" .
BreakStmt = "break" [ Label ] .
BuiltinArgs = Type [ "," ArgumentList ]
	| ArgumentList .
BuiltinCall = identifier_built_in "(" [ BuiltinArgs [ "," ] ] ")" .
Call = "(" [ ArgumentList [ "," ] ] ")" .
Channel = Expression .

//ChannelType = (
//		  "chan" [ "<-" ]
//		| "<-" "chan"
//	  ) ElementType .
//ChannelType = (
//		  "chan" [ comm ]
//		| comm "chan"
//	  ) ElementType .
ChannelType = (
		  "chan"
		  chancomm
		| commchan
	  ) ElementType .

CommCase = "case" ( SendStmt | RecvStmt )
	| "default" .
CommClause = CommCase ":" StatementList .
CompositeLit = LiteralType LiteralValue .
Condition = Expression .
ConstDecl = "const" (
		  ConstSpec
		| "(" { ConstSpec ";" } ")"
	  ) .

//ConstSpec = IdentifierList [
//		 [ Type ] "=" ExpressionList
//	  ] .
ConstSpec = identifier_list [
		 [ Type ] "=" ExpressionList
	  ] .

ContinueStmt = "continue" [ Label ] .
Conversion = Type "(" Expression [ "," ] ")" .
Declaration = ConstDecl
	| TypeDecl
	| VarDecl .
DeferStmt = "defer" Expression .
Element = [ Key ":" ] Value .
ElementIndex = Expression .
ElementList = Element { "," Element } .
ElementType = Type .
EmptyStmt = .
ExprCaseClause = ExprSwitchCase ":" StatementList .
ExprSwitchCase = "case" ExpressionList
	| "default" .
ExprSwitchStmt = "switch" [ SimpleStmt ";" ] [ Expression ] "{" { ExprCaseClause } "}" .
Expression = UnaryExpr
	| Expression Binary_op UnaryExpr .
ExpressionList = Expression { "," Expression } .
ExpressionStmt = Expression .
FallthroughStmt = "fallthrough" .

//FieldDecl = (
//		  IdentifierList Type
//		| AnonymousField
//	  ) [ Tag ] .
FieldDecl = (
		  identifier_list Type
		| AnonymousField
	  ) [ Tag ] .

FieldName = identifier_field .

//ForClause = [ InitStmt ] ";" [ Condition ] ";" [ PostStmt ] .
ForClause = InitStmt ";" [ Condition ] ";" PostStmt .

ForStmt = "for" [
		  Condition
		| ForClause
		| RangeClause
	  ] Block .
Function = Signature FunctionBody .
FunctionBody = Block .

//FunctionDecl = "func" FunctionName ( Function | Signature ) .
FunctionDecl = "func" name ( Function | Signature ) .

FunctionLit = "func" Function .

//FunctionName = identifier .

FunctionType = "func" Signature .
GoStmt = "go" Expression .
GotoStmt = "goto" Label .

//IdentifierList = identifier { "," identifier } .

IfStmt = "if" [ SimpleStmt ";" ] Expression Block [
		 "else" ( IfStmt | Block )
	  ] .
ImportDecl = "import" (
		  ImportSpec
		| "(" { ImportSpec ";" } ")"
	  ) .
ImportPath = string_lit .

//ImportSpec = [ "." | PackageName ] ImportPath .
ImportSpec = [ "." | name ] ImportPath .

//IncDecStmt = Expression ( "++" | "--" ) .
IncDecStmt = Expression ( inc | dec ) .

Index = "[" Expression "]" .
InitStmt = SimpleStmt .
InterfaceType = "interface" "{" { MethodSpec ";" } "}" .

//InterfaceTypeName = TypeName .

Key = FieldName | ElementIndex .
KeyType = Type .

//Label = identifier .
Label = identifier_label .

//LabeledStmt = Label ":" Statement .
LabeledStmt = identifier_label Statement .

Literal = BasicLit
	| CompositeLit
	| FunctionLit .

//LiteralType = StructType
//	| ArrayType
//	| "[" "..." "]" ElementType
//	| SliceType
//	| MapType
//	| TypeName .
LiteralType = StructType
	| ArrayType
	| "[" ddd "]" ElementType
	| SliceType
	| MapType
	| TypeName .

LiteralValue = "{" [ ElementList [ "," ] ] "}" .
MapType = "map" "[" KeyType "]" ElementType .

//MethodDecl = "func" Receiver MethodName ( Function | Signature ) .
MethodDecl = "func" Receiver name ( Function | Signature ) .

MethodExpr = ReceiverType "." MethodName .
MethodName = identifier_method .

//MethodSpec = MethodName Signature
//	| InterfaceTypeName .
MethodSpec = name Signature
	| name [ "." name ] .

//Operand = Literal
//	| OperandName
//	| MethodExpr
//	| "(" Expression ")" .
Operand = Literal
	| identifier_operand
	| MethodExpr
	| "(" Expression ")" .

//OperandName = identifier | QualifiedIdent .

//PackageClause = "package" PackageName .
PackageClause = "package" name .

//PackageName = identifier .

//ParameterDecl = [ IdentifierList ] [ "..." ] Type .
//ParameterDecl = [ IdentifierList ] [ ddd ] Type .
//ParameterDecl = [ identifier_list ] [ ddd ] Type .
ParameterDecl = [ identifier_list ] [ ddd ] Type .

ParameterList = ParameterDecl { "," ParameterDecl } .
Parameters = "(" [ ParameterList [ "," ] ] ")" .
PointerType = "*" BaseType .
PostStmt = SimpleStmt .

//PrimaryExpr = Operand
//	| Conversion
//	| BuiltinCall
//	| PrimaryExpr Selector
//	| PrimaryExpr Index
//	| PrimaryExpr Slice
//	| PrimaryExpr TypeAssertion
//	| PrimaryExpr Call .
PrimaryExpr = (
		  Operand
		| Conversion
		| BuiltinCall
	) {
		  Selector
		| Index
		| Slice
		| TypeAssertion
		| Call 
	} .

//QualifiedIdent = PackageName "." identifier .

//RangeClause = (
//		  ExpressionList "="
//		| IdentifierList ":="
//	  ) "range" Expression .
//RangeClause = (
//		  ExpressionList "="
//		| IdentifierList colas
//	  ) "range" Expression .
RangeClause = (
		  ExpressionList "="
		| identifier_list colas
	  ) "range" Expression .

//Receiver = "(" [ identifier ] [ "*" ] BaseTypeName ")" .
Receiver = "(" [ name ] [ "*" ] BaseTypeName ")" .

//ReceiverType = TypeName
//	| "(" "*" TypeName ")"
//	| "(" ReceiverType ")" .
ReceiverType = TypeName
	| "(" "*" TypeName ")"
	| "(" TypeName ")" .

RecvExpr = Expression .

//RecvStmt = [
//		  ExpressionList "="
//		| IdentifierList ":="
//	  ] RecvExpr .
//RecvStmt = [
//		  ExpressionList "="
//		| IdentifierList colas
//	  ] RecvExpr .
RecvStmt = [
		  ExpressionList "="
		| identifier_list colas
	  ] RecvExpr .

//Result = Parameters | Type .
Result = Parameters | TypeName | TypeLit .

ReturnStmt = "return" [ ExpressionList ] .
SelectStmt = "select" "{" { CommClause } "}" .

//Selector = "." identifier .
Selector = "." identifier_selector .

//SendStmt = Channel "<-" Expression .
SendStmt = Channel comm Expression .

//ShortVarDecl = IdentifierList ":=" ExpressionList .
//ShortVarDecl = IdentifierList colas ExpressionList .
ShortVarDecl = identifier_list colas ExpressionList .

//Signature = Parameters [ Result ] .
Signature = Parameters
	| Parameters Result .

SimpleStmt = EmptyStmt
	| ExpressionStmt
	| SendStmt
	| IncDecStmt
	| Assignment
	| ShortVarDecl .
Slice = "[" [ Expression ] ":" [ Expression ] "]" .
SliceType = "[" "]" ElementType .
SourceFile = PackageClause ";" { ImportDecl ";" } { TopLevelDecl ";" } .
Statement = Declaration
	| LabeledStmt
	| SimpleStmt
	| GoStmt
	| ReturnStmt
	| BreakStmt
	| ContinueStmt
	| GotoStmt
	| FallthroughStmt
	| Block
	| IfStmt
	| SwitchStmt
	| SelectStmt
	| ForStmt
	| DeferStmt .
StatementList = { Statement ";" } .
StructType = "struct" "{" { FieldDecl ";" } "}" .
SwitchStmt = ExprSwitchStmt | TypeSwitchStmt .
Tag = string_lit .
TopLevelDecl = Declaration
	| FunctionDecl
	| MethodDecl .
Type = TypeName
	| TypeLit
	| "(" Type ")" .
TypeAssertion = "." "(" Type ")" .
TypeCaseClause = TypeSwitchCase ":" StatementList .
TypeDecl = "type" (
		  TypeSpec
		| "(" { TypeSpec ";" } ")"
	  ) .
TypeList = Type { "," Type } .
TypeLit = ArrayType
	| StructType
	| PointerType
	| FunctionType
	| InterfaceType
	| SliceType
	| MapType
	| ChannelType .

//TypeName = identifier | QualifiedIdent .
TypeName = identifier_type .

//TypeSpec = identifier Type .
TypeSpec = name Type .

TypeSwitchCase = "case" TypeList
	| "default" .

//TypeSwitchGuard = [ identifier ":=" ] PrimaryExpr "." "(" "type" ")" .
//TypeSwitchGuard = [ identifier colas ] PrimaryExpr "." "(" "type" ")" .
TypeSwitchGuard = [ identifier_list colas ] PrimaryExpr "." "(" "type" ")" .

TypeSwitchStmt = "switch" [ SimpleStmt ";" ] TypeSwitchGuard "{" { TypeCaseClause } "}" .
UnaryExpr = PrimaryExpr
	| Unary_op UnaryExpr .
Value = Expression | LiteralValue .
VarDecl = "var" (
		  VarSpec
		| "(" { VarSpec ";" } ")"
	  ) .

//VarSpec = IdentifierList (
//		  Type [ "=" ExpressionList ]
//		| "=" ExpressionList
//	  ) .
VarSpec = identifier_list (
		  Type [ "=" ExpressionList ]
		| "=" ExpressionList
	  ) .
