/*

%left		OROR
%left		ANDAND
%left		EQ NE LE GE '<' '>'
%left		'+' '-' '|' '^'
%left		'*' '/' '%' '&' LSH RSH ANDNOT

%left	notLParen // in Signature
%left	'('
%left	notRParen // in Type
%left	')'

*/

andand			= "&&" .
andnot			= "&^" .
chancomm		= "chan" "<-" .
comm			= "<-".
commchan		= "<-" "chan" .
ddd			= "..." .
dec			= "--" .
eq			= "==" .
ge			= ">=" .
inc			= "++" .
le			= "<=" .
lsh			= "<<" .
ne			= "!=" .
oror			= "||" .
rsh			= ">>" .

Binary_op = oror
	| andand
	| eq
	| ne
	| "<"
	| le
	| ">"
	| ge
	| "+"
	| "-"
	| "|"
	| "^"
	| "*"
	| "/"
	| "%"
	| lsh
	| rsh
	| "&"
	| andnot .

Unary_op = "+"
	| "-"
	| "!"
	| "^"
	| "*"
	| "&"
	| comm .

idlist_colas	= . // identifier { "," identifier } colas .
identifier_list = . // Manually enabled in proper contexts.
pack_name	= . // Including the following '.'.
non_type_name	= . // not a type name
type_name	= . // a type name
built_in_name	= .
ocomma_rparen	= [ "," ] ")" .

//-----------------------------------------------------------------------------

add_op = "+"
	| "-"
	| "|"
	| "^" .
assign_op = [ add_op | mul_op ] "=" .
big_u_value = "\\" "U" hex_digit hex_digit hex_digit hex_digit hex_digit hex_digit hex_digit hex_digit .
byte_value = octal_byte_value | hex_byte_value .
decimal_digit = "0" … "9" .
decimal_lit = ( "1" … "9" ) { decimal_digit } .
decimals = decimal_digit { decimal_digit } .
escaped_char = "\\" (
		  "a"
		| "b"
		| "f"
		| "n"
		| "r"
		| "t"
		| "v"
		| "\\"
		| "'"
		| "\""
	  ) .
exponent = ( "e" | "E" ) [ "+" | "-" ] decimals .
float_lit = decimals "." [ decimals ] [ exponent ]
	| decimals exponent
	| "." decimals [ exponent ] .
hex_byte_value = "\\" "x" hex_digit hex_digit .
hex_digit = "0" … "9"
	| "A" … "F"
	| "a" … "f" .
hex_lit = "0" ( "x" | "X" ) hex_digit { hex_digit } .
imaginary_lit = ( decimals | float_lit ) "i" .
int_lit = decimal_lit
	| octal_lit
	| hex_lit .
interpreted_string_lit = "\"" { unicode_value | byte_value } "\"" .
little_u_value = "\\" "u" hex_digit hex_digit hex_digit hex_digit .
mul_op = "*"
	| "/"
	| "%"
	| lsh
	| rsh
	| "&"
	| andnot .
newline = .
octal_byte_value = "\\" octal_digit octal_digit octal_digit .
octal_digit = "0" … "7" .
octal_lit = "0" { octal_digit } .
raw_string_lit = "`" { unicode_char | newline } "`" .
rune_lit = "'" ( unicode_value | byte_value ) "'" .
string_lit = raw_string_lit | interpreted_string_lit .
unicode_char = .
unicode_value = unicode_char
	| little_u_value
	| big_u_value
	| escaped_char .

AnonymousField = [ "*" ] TypeName .
ArgumentList = ExpressionList [ ddd ] .
ArrayLength = Expression .
ArrayType = "[" ArrayLength "]" ElementType .
Assignment = ExpressionList assign_op ExpressionList .
BaseType = Type .
BaseTypeName = type_name .
BasicLit = int_lit
	| float_lit
	| imaginary_lit
	| rune_lit
	| string_lit .
Block = "{" StatementList "}" .
BreakStmt = "break" [ Label ] .
BuiltinArgs = Type [ "," ArgumentList ]
	| ArgumentList .
BuiltinCall = built_in_name "(" BuiltinArgs ocomma_rparen .
Call = "(" ")"
	| "(" ArgumentList ocomma_rparen .
Channel = Expression .
ChannelType = (
		  "chan"
		| chancomm
		| commchan
	  ) ElementType .
CommCase = "case" ( SendStmt | RecvStmt )
	| "default" .
CommClause = CommCase ":" StatementList .
CompositeLit = LiteralType LiteralValue .
Condition = Expression .
ConstDecl = "const" (
		  ConstSpec
		| "(" { ConstSpec ";" } ")"
	  ) .
ConstSpec = identifier_list [
		 [ Type ] "=" ExpressionList
	  ] .
ContinueStmt = "continue" [ Label ] .
Conversion = Type "(" Expression ocomma_rparen .
Declaration = ConstDecl
	| TypeDecl
	| VarDecl .
DeferStmt = "defer" Expression .
Element = [ Key ":" ] Value .
ElementList = Element { "," Element } .
ElementType = Type .
EmptyStmt = .
ExprCaseClause = ExprSwitchCase ":" StatementList .
ExprSwitchCase = "case" ExpressionList
	| "default" .
ExprSwitchStmt = "switch" [ SimpleStmt ";" ] [ Expression ] "{" { ExprCaseClause } "}" .
Expression = UnaryExpr
	| Expression Binary_op Expression .
ExpressionList = Expression { "," Expression } .
ExpressionStmt = Expression .
FallthroughStmt = "fallthrough" .
FieldDecl = (
		  identifier_list Type
		| AnonymousField
	  ) [ Tag ] .
ForClause = InitStmt ";" [ Condition ] ";" PostStmt .
ForStmt = "for" [
		  Condition
		| ForClause
		| RangeClause
	  ] Block .
Function = Signature FunctionBody .
FunctionBody = Block .
FunctionDecl = "func" FunctionName ( Function | Signature ) .
FunctionLit = "func" Function .
FunctionName = Name .
FunctionType = "func" Signature .
GoStmt = "go" Expression .
GotoStmt = "goto" Label .
IfStmt = "if" [ SimpleStmt ";" ] Expression Block [
		 "else" ( IfStmt | Block )
	  ] .
ImportDecl = "import" (
		  ImportSpec
		| "(" { ImportSpec ";" } ")"
	  ) .
ImportPath = string_lit .
ImportSpec = [ "." | Name ] ImportPath .
IncDecStmt = Expression ( inc | dec ) .
Index = "[" Expression "]" .
InitStmt = SimpleStmt .
InterfaceType = "interface" "{" { MethodSpec ";" } "}" .
InterfaceTypeName = TypeName .
Key = Expression .
KeyType = Type .
Label = Name .
LabeledStmt = Label ":" Statement .
Literal = BasicLit
	| CompositeLit
	| FunctionLit .
LiteralType = StructType
	| ArrayType
	| "[" ddd "]" ElementType
	| SliceType
	| MapType
	| TypeName .
LiteralValue = "{" [ ElementList [ "," ] ] "}" .
MapType = "map" "[" KeyType "]" ElementType .
MethodDecl = "func" Receiver MethodName ( Function | Signature ) .
MethodExpr = ReceiverType "." MethodName .
MethodName = Name .
MethodSpec = MethodName Signature
	| InterfaceTypeName .
Name = non_type_name | type_name .
Operand = Literal
	| OperandName
	| MethodExpr
	| "(" Expression ")" .
OperandName = [ pack_name ] non_type_name .
PackageClause = "package" Name .
ParameterDecl = [ identifier_list ] [ ddd ] Type . //TODO
ParameterList = ParameterDecl { "," ParameterDecl } .
Parameters = "(" ")"
	| "(" ParameterList ocomma_rparen .
PointerType = "*" BaseType .
PostStmt = SimpleStmt .
PrimaryExpr = Operand
	| Conversion
	| BuiltinCall
	| PrimaryExpr Selector
	| PrimaryExpr Index
	| PrimaryExpr Slice
	| PrimaryExpr TypeAssertion
	| PrimaryExpr Call .
RangeClause = (
		  ExpressionList "="
		| idlist_colas
	  ) "range" Expression .
Receiver = "(" [ Name ] [ "*" ] BaseTypeName ")" .
ReceiverType = TypeName
	| "(" "*" TypeName ")"
	| "(" TypeName ")" .
RecvExpr = Expression .
RecvStmt = [
		  ExpressionList "="
		| idlist_colas
	  ] RecvExpr .
Result = Parameters | TypeName | TypeLit .
ReturnStmt = "return" [ ExpressionList ] .
SelectStmt = "select" "{" { CommClause } "}" .
Selector = "." Name .
SendStmt = Channel comm Expression .
ShortVarDecl = idlist_colas ExpressionList .
Signature = Parameters [ Result ] .
SimpleStmt = EmptyStmt
	| ExpressionStmt
	| SendStmt
	| IncDecStmt
	| Assignment
	| ShortVarDecl .
Slice = "[" [ Expression ] ":" [ Expression ] "]" .
SliceType = "[" "]" ElementType .
SourceFile = PackageClause ";" { ImportDecl ";" } { TopLevelDecl ";" } .
Statement = Declaration
	| LabeledStmt
	| SimpleStmt
	| GoStmt
	| ReturnStmt
	| BreakStmt
	| ContinueStmt
	| GotoStmt
	| FallthroughStmt
	| Block
	| IfStmt
	| SwitchStmt
	| SelectStmt
	| ForStmt
	| DeferStmt .
StatementList = { Statement ";" } .
StructType = "struct" "{" { FieldDecl ";" } "}" .
SwitchStmt = ExprSwitchStmt | TypeSwitchStmt .
Tag = string_lit .
TopLevelDecl = Declaration
	| FunctionDecl
	| MethodDecl .
Type = TypeName
	| TypeLit
	| "(" TypeName ")" 
	| "(" TypeLit ")" .
TypeAssertion = "." "(" Type ")" .
TypeCaseClause = TypeSwitchCase ":" StatementList .
TypeDecl = "type" (
		  TypeSpec
		| "(" { TypeSpec ";" } ")"
	  ) .
TypeList = Type { "," Type } .
TypeLit = ArrayType
	| StructType
	| PointerType
	| FunctionType
	| InterfaceType
	| SliceType
	| MapType
	| ChannelType .
TypeName = [ pack_name ] type_name .
TypeSpec = Name Type .
TypeSwitchCase = "case" TypeList
	| "default" .
TypeSwitchGuard = [ idlist_colas ] PrimaryExpr "." "(" "type" ")" .
TypeSwitchStmt = "switch" [ SimpleStmt ";" ] TypeSwitchGuard "{" { TypeCaseClause } "}" .
UnaryExpr = PrimaryExpr
	| Unary_op UnaryExpr .
Value = Expression | LiteralValue .
VarDecl = "var" (
		  VarSpec
		| "(" { VarSpec ";" } ")"
	  ) .
VarSpec = identifier_list (
		  Type [ "=" ExpressionList ]
		| "=" ExpressionList
	  ) .
