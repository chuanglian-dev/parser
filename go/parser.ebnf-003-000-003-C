/* "C"

go1.1.1.ebnf

ebnf2y -M -o parser.y parser.ebnf:		34	+28	=62
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	19	+38	=57

...............................................................................
named tokens, Binary_op, Unary_op

ebnf2y -M -o parser.y parser.ebnf:		37	+37	=74
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	20	+39	=59

...............................................................................
ForClause,commchan,chancomm,idlist_colas

ebnf2y -M -o parser.y parser.ebnf:		25	+16	=41
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	12	+16	=28

...............................................................................
-IdentifierList,+identifier_list

ebnf2y -M -o parser.y parser.ebnf:		23	+16	=39
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	12	+15	=27

...............................................................................
pack_name,type_name

ebnf2y -M -o parser.y parser.ebnf:		20	+3	=23
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	 9	+1	=10

...............................................................................
Key

ebnf2y -M -o parser.y parser.ebnf:		19	+2	=21
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	 7	+1	= 8

...............................................................................
BaseTypeName,ReceiverType

ebnf2y -M -o parser.y parser.ebnf:		18	+2	=20
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	 8	+0	= 8

...............................................................................
Result

ebnf2y -M -o parser.y parser.ebnf:		8	+0	=8
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	7	+0	=7

...............................................................................
BuiltinCall

ebnf2y -M -o parser.y parser.ebnf:		6	+0	=6
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	5	+0	=5

...............................................................................
BuiltinCall/II., ocomma_rparen

ebnf2y -M -o parser.y parser.ebnf:		4	+0	=4
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	4	+0	=4

...............................................................................
Type

ebnf2y -M -o parser.y parser.ebnf:		3	+0	=3
ebnf2y -M -iy 1 -o parser.y parser.ebnf:	3	+0	=3

*/


andand			= "&&" .
andnot			= "&^" .
chancomm		= "chan" "<-" .
//colas			= ":=" .
comm			= "<-".
commchan		= "<-" "chan" .
ddd			= "..." .
dec			= "--" .
eq			= "==" .
ge			= ">=" .
inc			= "++" .
le			= "<=" .
lsh			= "<<" .
ne			= "!=" .
oror			= "||" .
rsh			= ">>" .

Binary_op = oror
	| andand
	| eq
	| ne
	| "<"
	| le
	| ">"
	| ge
	| "+"
	| "-"
	| "|"
	| "^"
	| "*"
	| "/"
	| "%"
	| lsh
	| rsh
	| "&"
	| andnot .

Unary_op = "+"
	| "-"
	| "!"
	| "^"
	| "*"
	| "&"
	| comm .

idlist_colas	= . // identifier { "," identifier } colas .
identifier_list = . // Manually enabled in proper contexts.
pack_name	= .
type_name	= .
built_in_name	= .
ocomma_rparen	= [ "," ] ")" .

//-----------------------------------------------------------------------------

add_op = "+"
	| "-"
	| "|"
	| "^" .
assign_op = [ add_op | mul_op ] "=" .
big_u_value = "\\" "U" hex_digit hex_digit hex_digit hex_digit hex_digit hex_digit hex_digit hex_digit .

//binary_op = "||"
//	| "&&"
//	| rel_op
//	| add_op
//	| mul_op .

byte_value = octal_byte_value | hex_byte_value .
decimal_digit = "0" … "9" .
decimal_lit = ( "1" … "9" ) { decimal_digit } .
decimals = decimal_digit { decimal_digit } .
escaped_char = "\\" (
		  "a"
		| "b"
		| "f"
		| "n"
		| "r"
		| "t"
		| "v"
		| "\\"
		| "'"
		| "\""
	  ) .
exponent = ( "e" | "E" ) [ "+" | "-" ] decimals .
float_lit = decimals "." [ decimals ] [ exponent ]
	| decimals exponent
	| "." decimals [ exponent ] .
hex_byte_value = "\\" "x" hex_digit hex_digit .
hex_digit = "0" … "9"
	| "A" … "F"
	| "a" … "f" .
hex_lit = "0" ( "x" | "X" ) hex_digit { hex_digit } .
identifier = letter { letter | unicode_digit } .
imaginary_lit = ( decimals | float_lit ) "i" .
int_lit = decimal_lit
	| octal_lit
	| hex_lit .
interpreted_string_lit = "\"" { unicode_value | byte_value } "\"" .
letter = unicode_letter | "_" .
little_u_value = "\\" "u" hex_digit hex_digit hex_digit hex_digit .

//mul_op = "*"
//	| "/"
//	| "%"
//	| "<<"
//	| ">>"
//	| "&"
//	| "&^" .
mul_op = "*"
	| "/"
	| "%"
	| lsh
	| rsh
	| "&"
	| andnot .

newline = .
octal_byte_value = "\\" octal_digit octal_digit octal_digit .
octal_digit = "0" … "7" .
octal_lit = "0" { octal_digit } .
raw_string_lit = "`" { unicode_char | newline } "`" .

//rel_op = "=="
//	| "!="
//	| "<"
//	| "<="
//	| ">"
//	| ">=" .

rune_lit = "'" ( unicode_value | byte_value ) "'" .
string_lit = raw_string_lit | interpreted_string_lit .

//unary_op = "+"
//	| "-"
//	| "!"
//	| "^"
//	| "*"
//	| "&"
//	| "<-" .

unicode_char = .
unicode_digit = .
unicode_letter = .
unicode_value = unicode_char
	| little_u_value
	| big_u_value
	| escaped_char .

AnonymousField = [ "*" ] TypeName .

//ArgumentList = ExpressionList [ "..." ] .
ArgumentList = ExpressionList [ ddd ] .

ArrayLength = Expression .
ArrayType = "[" ArrayLength "]" ElementType .
Assignment = ExpressionList assign_op ExpressionList .
BaseType = Type .

//BaseTypeName = identifier .
BaseTypeName = type_name .

BasicLit = int_lit
	| float_lit
	| imaginary_lit
	| rune_lit
	| string_lit .
Block = "{" StatementList "}" .
BreakStmt = "break" [ Label ] .
BuiltinArgs = Type [ "," ArgumentList ]
	| ArgumentList .

//BuiltinCall = identifier "(" [ BuiltinArgs [ "," ] ] ")" .
//BuiltinCall = built_in_name "(" [ BuiltinArgs [ "," ] ] ")" .
//BuiltinCall = built_in_name "(" BuiltinArgs [ "," ] ")" .
BuiltinCall = built_in_name "(" BuiltinArgs ocomma_rparen .

//Call = "(" [ ArgumentList [ "," ] ] ")" .
Call = "(" ")"
	| "(" ArgumentList ocomma_rparen .

Channel = Expression .

//ChannelType = (
//		  "chan" [ "<-" ]
//		| "<-" "chan"
//	  ) ElementType .
//ChannelType = (
//		  "chan" [ comm ]
//		| comm "chan"
//	  ) ElementType .
ChannelType = (
		  "chan"
		| chancomm
		| commchan
	  ) ElementType .

CommCase = "case" ( SendStmt | RecvStmt )
	| "default" .
CommClause = CommCase ":" StatementList .
CompositeLit = LiteralType LiteralValue .
Condition = Expression .
ConstDecl = "const" (
		  ConstSpec
		| "(" { ConstSpec ";" } ")"
	  ) .

//ConstSpec = IdentifierList [
//		 [ Type ] "=" ExpressionList
//	  ] .
ConstSpec = identifier_list [
		 [ Type ] "=" ExpressionList
	  ] .

ContinueStmt = "continue" [ Label ] .

//Conversion = Type "(" Expression [ "," ] ")" .
Conversion = Type "(" Expression ocomma_rparen .

Declaration = ConstDecl
	| TypeDecl
	| VarDecl .
DeferStmt = "defer" Expression .
Element = [ Key ":" ] Value .

//ElementIndex = Expression .

ElementList = Element { "," Element } .
ElementType = Type .
EmptyStmt = .
ExprCaseClause = ExprSwitchCase ":" StatementList .
ExprSwitchCase = "case" ExpressionList
	| "default" .
ExprSwitchStmt = "switch" [ SimpleStmt ";" ] [ Expression ] "{" { ExprCaseClause } "}" .
Expression = UnaryExpr
	| Expression Binary_op UnaryExpr .
ExpressionList = Expression { "," Expression } .
ExpressionStmt = Expression .
FallthroughStmt = "fallthrough" .

//FieldDecl = (
//		  IdentifierList Type
//		| AnonymousField
//	  ) [ Tag ] .
FieldDecl = (
		  identifier_list Type
		| AnonymousField
	  ) [ Tag ] .

//FieldName = identifier .

//ForClause = [ InitStmt ] ";" [ Condition ] ";" [ PostStmt ] .
ForClause = InitStmt ";" [ Condition ] ";" PostStmt .

ForStmt = "for" [
		  Condition
		| ForClause
		| RangeClause
	  ] Block .
Function = Signature FunctionBody .
FunctionBody = Block .
FunctionDecl = "func" FunctionName ( Function | Signature ) .
FunctionLit = "func" Function .
FunctionName = identifier .
FunctionType = "func" Signature .
GoStmt = "go" Expression .
GotoStmt = "goto" Label .

//IdentifierList = identifier { "," identifier } .

IfStmt = "if" [ SimpleStmt ";" ] Expression Block [
		 "else" ( IfStmt | Block )
	  ] .
ImportDecl = "import" (
		  ImportSpec
		| "(" { ImportSpec ";" } ")"
	  ) .
ImportPath = string_lit .
ImportSpec = [ "." | PackageName ] ImportPath .

//IncDecStmt = Expression ( "++" | "--" ) .
IncDecStmt = Expression ( inc | dec ) .

Index = "[" Expression "]" .
InitStmt = SimpleStmt .
InterfaceType = "interface" "{" { MethodSpec ";" } "}" .
InterfaceTypeName = TypeName .

//Key = FieldName | ElementIndex .
Key = Expression .

KeyType = Type .
Label = identifier .
LabeledStmt = Label ":" Statement .
Literal = BasicLit
	| CompositeLit
	| FunctionLit .

//LiteralType = StructType
//	| ArrayType
//	| "[" "..." "]" ElementType
//	| SliceType
//	| MapType
//	| TypeName .
LiteralType = StructType
	| ArrayType
	| "[" ddd "]" ElementType
	| SliceType
	| MapType
	| TypeName .

LiteralValue = "{" [ ElementList [ "," ] ] "}" .
MapType = "map" "[" KeyType "]" ElementType .
MethodDecl = "func" Receiver MethodName ( Function | Signature ) .
MethodExpr = ReceiverType "." MethodName .
MethodName = identifier .
MethodSpec = MethodName Signature
	| InterfaceTypeName .
Operand = Literal
	| OperandName
	| MethodExpr
	| "(" Expression ")" .

//OperandName = identifier | QualifiedIdent .
OperandName = [ pack_name ] identifier .

PackageClause = "package" PackageName .
PackageName = identifier .

//ParameterDecl = [ IdentifierList ] [ "..." ] Type .
//ParameterDecl = [ IdentifierList ] [ ddd ] Type .
ParameterDecl = [ identifier_list ] [ ddd ] Type . //TODO

ParameterList = ParameterDecl { "," ParameterDecl } .

//Parameters = "(" [ ParameterList [ "," ] ] ")" .
Parameters = "(" ")"
	| "(" ParameterList ocomma_rparen .

PointerType = "*" BaseType .
PostStmt = SimpleStmt .
PrimaryExpr = Operand
	| Conversion
	| BuiltinCall
	| PrimaryExpr Selector
	| PrimaryExpr Index
	| PrimaryExpr Slice
	| PrimaryExpr TypeAssertion
	| PrimaryExpr Call .

//QualifiedIdent = PackageName "." identifier .

//RangeClause = (
//		  ExpressionList "="
//		| IdentifierList ":="
//	  ) "range" Expression .
//RangeClause = (
//		  ExpressionList "="
//		| IdentifierList colas
//	  ) "range" Expression .
RangeClause = (
		  ExpressionList "="
		| idlist_colas
	  ) "range" Expression .

Receiver = "(" [ identifier ] [ "*" ] BaseTypeName ")" .

//ReceiverType = TypeName
//	| "(" "*" TypeName ")"
//	| "(" ReceiverType ")" .
ReceiverType = TypeName
	| "(" "*" TypeName ")"
	| "(" TypeName ")" .

RecvExpr = Expression .

//RecvStmt = [
//		  ExpressionList "="
//		| IdentifierList ":="
//	  ] RecvExpr .
//RecvStmt = [
//		  ExpressionList "="
//		| IdentifierList colas
//	  ] RecvExpr .
RecvStmt = [
		  ExpressionList "="
		| idlist_colas
	  ] RecvExpr .

//Result = Parameters | Type .
Result = Parameters | TypeName | TypeLit .

ReturnStmt = "return" [ ExpressionList ] .
SelectStmt = "select" "{" { CommClause } "}" .
Selector = "." identifier .

//SendStmt = Channel "<-" Expression .
SendStmt = Channel comm Expression .

//ShortVarDecl = IdentifierList ":=" ExpressionList .
//ShortVarDecl = IdentifierList colas ExpressionList .
ShortVarDecl = idlist_colas ExpressionList .

Signature = Parameters [ Result ] .
SimpleStmt = EmptyStmt
	| ExpressionStmt
	| SendStmt
	| IncDecStmt
	| Assignment
	| ShortVarDecl .
Slice = "[" [ Expression ] ":" [ Expression ] "]" .
SliceType = "[" "]" ElementType .
SourceFile = PackageClause ";" { ImportDecl ";" } { TopLevelDecl ";" } .
Statement = Declaration
	| LabeledStmt
	| SimpleStmt
	| GoStmt
	| ReturnStmt
	| BreakStmt
	| ContinueStmt
	| GotoStmt
	| FallthroughStmt
	| Block
	| IfStmt
	| SwitchStmt
	| SelectStmt
	| ForStmt
	| DeferStmt .
StatementList = { Statement ";" } .
StructType = "struct" "{" { FieldDecl ";" } "}" .
SwitchStmt = ExprSwitchStmt | TypeSwitchStmt .
Tag = string_lit .
TopLevelDecl = Declaration
	| FunctionDecl
	| MethodDecl .

//Type = TypeName
//	| TypeLit
//	| "(" Type ")" .
Type = TypeName
	| TypeLit
	| "(" TypeName ")" 
	| "(" TypeLit ")" .

TypeAssertion = "." "(" Type ")" .
TypeCaseClause = TypeSwitchCase ":" StatementList .
TypeDecl = "type" (
		  TypeSpec
		| "(" { TypeSpec ";" } ")"
	  ) .
TypeList = Type { "," Type } .
TypeLit = ArrayType
	| StructType
	| PointerType
	| FunctionType
	| InterfaceType
	| SliceType
	| MapType
	| ChannelType .

//TypeName = identifier | QualifiedIdent .
TypeName = [ pack_name ] type_name .

TypeSpec = identifier Type .
TypeSwitchCase = "case" TypeList
	| "default" .

//TypeSwitchGuard = [ identifier ":=" ] PrimaryExpr "." "(" "type" ")" .
//TypeSwitchGuard = [ identifier colas ] PrimaryExpr "." "(" "type" ")" .
TypeSwitchGuard = [ idlist_colas ] PrimaryExpr "." "(" "type" ")" .

TypeSwitchStmt = "switch" [ SimpleStmt ";" ] TypeSwitchGuard "{" { TypeCaseClause } "}" .
UnaryExpr = PrimaryExpr
	| Unary_op UnaryExpr .
Value = Expression | LiteralValue .
VarDecl = "var" (
		  VarSpec
		| "(" { VarSpec ";" } ")"
	  ) .

//VarSpec = IdentifierList (
//		  Type [ "=" ExpressionList ]
//		| "=" ExpressionList
//	  ) .
VarSpec = identifier_list (
		  Type [ "=" ExpressionList ]
		| "=" ExpressionList
	  ) .
